plugins {
    id "java"
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

group = rootProject.group
version = rootProject.version
sourceCompatibility = 1.8

dependencies {
}

javafx {
    version = "17"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
    configuration = 'compileOnly'
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task sourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    getArchiveClassifier().set('javadoc')
    from javadoc
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId = "io.github.typhon0"
            artifactId = "AnimateFX"
            version = project.version
            pom {
                description = 'A library of ready-to-use animations for JavaFX'
                url = 'https://github.com/typhon0/AnimateFX'
                name = "AnimateFX"
                version=project.version
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = 'typhon0'
                        name = 'Lo√Øc Sculier'
                        email = 'sculier.loic@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/Typhon0/AnimateFX.git'
                    developerConnection = 'scm:git:ssh://github.com/Typhon0/AnimateFX.git'
                    url = 'https://github.com/Typhon0/AnimateFX/tree/master'
                }
            }
        }
    }

}


