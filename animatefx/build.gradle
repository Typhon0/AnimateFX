plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id "com.jfrog.artifactory" version"4.18.0"
    id "maven-publish"
    id "java"
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    jcenter()
}

version = '1.2.2' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
sourceCompatibility = 1.8

dependencies {
}

javafx {
    version = "11"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web' ]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "typhon0"
            name "Lo√Øc Sculier"
            email "sculier.loic@gmail.com"
        }
    }

    scm {
        url "https://github.com/typhon0/animatefx"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'io.github.typhon0'
            artifactId 'AnimateFX'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A library of ready-to-use animations for JavaFX')
                root.appendNode('name', 'AnimateFX')
                root.appendNode('url', 'https://github.com/typhon0/AnimateFX')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'AnimateFX'
        name = 'animatefx'
        userOrg = 'typhon0'
        licenses = ['Apache-2.0']
        publish = true
        vcsUrl = 'https://github.com/typhon0/animatefx.git'
        version {
            name = project.version
            desc = System.getProperty("build.number")
            released  = new Date()
        }
    }

}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getProperty('bintray.user')
            password = System.getProperty('bintray.key')
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(System.getProperty('build.number'))
}
